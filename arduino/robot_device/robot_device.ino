// Tracking settings
const int trigAllPin = 5;  // All receivers are connected to this pin
const int kLeftEchoPin = 6;  // needs to be between 2 and 7 (inclusive)
const int kRightEchoPin = 7;  // needs to be between 2 and 7 (inclusive)
const uint32_t kReceiverTimeout = 110000;


#define DEBUG 1

// Masks for echo pins
const uint8_t kLeft = (1 << kLeftEchoPin);
const uint8_t kRight = (1 << kRightEchoPin);
const uint8_t kBoth = kLeft | kRight;

// Radio settings
const uint32_t kRadioDelay = 10000;  // Time of radio communication in microseconds
const String kRadioRequestCode = "$$$";

// Comm Settings
// Code for no signal received
const unsigned long noSig = 0xFFFFFFFF;
// Code identifies beginning of message
const unsigned long openMsg = 0xFFFFFFFE;
// Code identifies end of message
const unsigned long closeMsg = 0xFFFFFFFD;

// This ID identifies ultrasonic data
const unsigned long uID = 0xFFFFFFFC;

struct timeData {
    uint32_t leftUD;
    uint32_t rightUD;
};


/*
 * Used to activate the receivers
 */
void activateUltrasonic(int trigPin);
/*
 * Sends radio signal to user's device to cause it to emit an ultrasonic pulse.
 */
void requestSignal();
/*
 * Waits for signal to be received on both receivers and returns the time since
 * `startTime` it took in the `timeLeft` and `timeRight` pointers. If no signal
 * is received, the respective output is set to 0xFFFFFFFF (2^32 - 1).
 */
void measureTimes(uint32_t startTime, uint32_t* timeLeft, uint32_t* timeRight);
/*
 * Uses port manipulation to read all echo pins (specified by the `mask`)
 * extremely fast (~0.1 us).
 * (`PIND` is a byte containing values of pins 0-7.)
 */
uint8_t getEcho(uint8_t mask) {return PIND & mask;}


/*
 * Sends data generated by sensors over serial
 */

void sendData(struct timeData td);

void setup() 
{
  // Radio setup
  pinMode(8,OUTPUT); // switch off the radio
  digitalWrite(8,HIGH);
  pinMode(4,OUTPUT); // switch on the radio
  digitalWrite(4,LOW); // ensure the radio is not sleeping
  Serial.begin(9600);

  // Tracking pins setup
  pinMode(trigAllPin, OUTPUT);
  digitalWrite(trigAllPin, LOW);
  pinMode(kLeftEchoPin, INPUT);
  pinMode(kRightEchoPin, INPUT);
}

void loop() 
{
  activateUltrasonic(trigAllPin);
  delayMicroseconds(600);
  uint32_t t0 = micros();
  // Both echo pins should be high now
  if (getEcho(kBoth) != kBoth)
    Serial.println("ERROR: Unexpected echo state");
  requestSignal();

  // Now we have about 20 ms to do stuff

  struct timeData td;
  measureTimes(t0, &(td.leftUD), &(td.rightUD));

  // Now we have td.leftUD and td.rightUD. We can send them to EV3 or store them
  // in global variables and send in the next iteration. We can also do other
  // stuff here.

  // Send data over serial to the ev3 brick
  digitalWrite(8,LOW);
  sendData(td);
  delay(1);
  digitalWrite(8,HIGH);

  delay(1000);  // TODO: should probably be removed or reduced
}

void activateUltrasonic(int trigPin)
{
  digitalWrite(trigPin, LOW);
  delayMicroseconds(2);
  digitalWrite(trigPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(trigPin, LOW);
}

void requestSignal() {
  Serial.println(kRadioRequestCode);
  Serial.flush();
}

void measureTimes(uint32_t startTime, uint32_t* timeLeft, uint32_t* timeRight)
{
  uint8_t echo = getEcho(kBoth);
  // Both echo pins should still be high
  if (echo != kBoth)
    Serial.println("ERROR: Listening too late");

  *timeLeft = 0xFFFFFFFF;   // Max number
  *timeRight = 0xFFFFFFFF;  // Max number
  // Wait for echo pins to go low and note times
  while (echo != 0x00) {
    echo = getEcho(kBoth);
    if ((*timeLeft == 0xFFFFFFFF) && ((echo & kLeft) == 0))
      *timeLeft = micros() - startTime;
    if ((*timeRight == 0xFFFFFFFF) && ((echo & kRight) == 0))
      *timeRight = micros() - startTime;
  }

  // Check for timeouts
  if (*timeLeft > kReceiverTimeout) *timeLeft = 0xFFFFFFFF;
  if (*timeRight > kReceiverTimeout) *timeRight = 0xFFFFFFFF;

  // Check radio delay
  if (*timeLeft < kRadioDelay) {
    Serial.println("ERROR: Radio delay too high. timeLeft=");
    Serial.println(*timeLeft);
    *timeLeft = 0xFFFFFFFF;
  }
  if (*timeRight < kRadioDelay) {
    Serial.println("ERROR: Radio delay too high. timeRight=");
    Serial.println(*timeRight);
    *timeRight = 0xFFFFFFFF;
  }

  // Adjust for radio delay
  if (*timeLeft != 0xFFFFFFFF)
    *timeLeft -= kRadioDelay;
  if (*timeRight!= 0xFFFFFFFF)
    *timeRight -= kRadioDelay;
}

void sendData(struct timeData td) {
    Serial.println(String(openMsg));
    Serial.flush();
    Serial.println(String(uID));
    Serial.flush();
    Serial.println(String(td.leftUD));
    Serial.flush();
    Serial.println(String(td.rightUD));
    Serial.flush();
    Serial.println(String(closeMsg));
    Serial.flush();
}
